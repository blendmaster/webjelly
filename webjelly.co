# Webjelly
# Author: Steven Ruppert
# For the Fall 2012 CSCI441 Graphics class at Colorado School of Mines

# this comment appears in the compiled source:
/* This file (webjelly.js) is compiled from webjelly.co. Please view the
original commented source there. */

"use strict"

canvas = document.getElementById \canvas

try
  gl = canvas.getContext \webgl or canvas.getContext \experimental-webgl

if gl!?
  alert "Sorry, it looks like your browser doesn't support WebGL, or webGL is
    disabled!"
  throw new Error "no webgl ;_;"

# I don't like typing in gl all the time, so I will attach everything to the
# global context. Don't do this at home, kids.
for k, v in gl
  window[k] =
    if typeof v is \function
      v.bind gl
    else
      v

# init the shader program
clearColor 0 0 0 1 # black
enable     DEPTH_TEST
depthFunc  LEQUAL
clear      COLOR_BUFFER_BIT | DEPTH_BUFFER_BIT

fragment-shader = createShader FRAGMENT_SHADER
  shaderSource (&), """

    void main(void) {
      gl_FragColor = vec4(1.0, 1.0, 1.0, 1.0);
    }

  """
  compileShader(&)
  unless getShaderParameter (&), COMPILE_STATUS
    throw new Error "couldn't compile fragment shader!"

vertex-shader = createShader VERTEX_SHADER
  shaderSource (&), """

    attribute vec3 aVertexPosition;

    uniform mat4 uMVMatrix;
    uniform mat4 uPMatrix;

    void main(void) {
      gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
    }
  """
  compileShader (&)
  unless getShaderParameter (&), COMPILE_STATUS
    throw new Error "couldn't compile vertex shader!"

program = createProgram!
  attachShader (&), vertex-shader
  attachShader (&), fragment-shader
  linkProgram (&)
  unless getProgramParameter (&), LINK_STATUS
    throw new Error "couldn't intialize shader program!"
  useProgram (&)

# draw the model from the input file of triangles and vertices
draw = !(triangles, vertices) ->

  vertices-buffer = createBuffer!
    bindBuffer ARRAY_BUFFER, (&)
    bufferData ARRAY_BUFFER, vertices, STATIC_DRAW

  getAttribLocation program, \aVertexPosition
    enableVertexAttribArray (&)
    vertexAttribPointer (&), 3, FLOAT, false 0 0

  uniformMatrix4fv do
    getUniformLocation program, \uPMatrix
    false
    makePerspective 45 640/480 0.1 100 .floats

  uniformMatrix4fv do
    getUniformLocation program, \uMVMatrix
    false
    (Matrix.I 4).x (Matrix.Translation $V [0 0 -6] .ensure4x4!) .floats

  drawArrays TRIANGLES, 0 vertices.length

# read the input file
parse = !->
  if @files.0
    new FileReader
      &readAsText that
      &onload = !->
        tokens = @result.split /\s+/ .map parseFloat
        [num-triangles, num-vertices] = tokens.splice 0, 2

        draw do
          tokens.splice 0 num-triangles * 3
          new Float32Array do tokens.splice 0 num-vertices * 3

document.getElementById \file
  &addEventListener \change parse
  # on refresh, if some file is still selected, do parse
  parse.call (&)
